plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

loom {
    enableTransitiveAccessWideners.set(true)
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
    mavenCentral()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation "net.fabricmc:fabric-language-kotlin:$rootProject.fabric_kotlin"
    modImplementation "com.cobblemon:fabric:$rootProject.cobblemon_version"
    modImplementation "software.bernie.geckolib:geckolib-fabric-$rootProject.geckolib_version"
    include(modApi "me.shedaniel.cloth:cloth-config-fabric:$rootProject.cloth_config_version")

    modImplementation "com.terraformersmc:modmenu:11.0.3"
    modRuntimeOnly "maven.modrinth:jade:$rootProject.jade_version+fabric"
    // modRuntimeOnly "lol.bai:badpackets:fabric-$rootProject.badpackets_version"
    runtimeOnly("org.reflections:reflections:0.10.2")
    runtimeOnly("org.javassist:javassist:3.29.2-GA")
    modRuntimeOnly "curse.maven:yungs-api-fabric-421649:6715461"
    modRuntimeOnly "curse.maven:worldedit-225608:5830452"
    modCompileOnly "mcp.mobius.waila:wthit:fabric-$rootProject.wthit_version"
    modRuntimeOnly "io.wispforest:accessories-fabric:1.1.0-beta.52+1.21.1"
    modImplementation "curse.maven:cobblemon-mega-showdown-1189523:7575682"
    modImplementation "curse.maven:cobbledollars-859232:6604561"
    modRuntimeOnly "maven.modrinth:supermartijn642s-config-lib:1.1.8-fabric-mc1.21"
    modImplementation "curse.maven:radical-cobblemon-trainers-api-1152792:7257754"
    modRuntimeOnly "dev.architectury:architectury-fabric:13.0.8"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    var replaceProperties = [
        display_name: rootProject.display_name,
        description: rootProject.mod_description,
        archives_name: rootProject.archives_name,
        version: project.version,
        minecraft_version: rootProject.minecraft_version,
        cobblemon_version: rootProject.cobblemon_dependency,
        geckolib_version: rootProject.geckolib_dependency
    ]
    inputs.properties replaceProperties

    filesMatching('fabric.mod.json') {
        expand  replaceProperties
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
}