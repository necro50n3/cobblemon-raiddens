plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

loom {
    enableTransitiveAccessWideners.set(true)
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url 'https://maven.impactdev.net/repository/development/' }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"
    modImplementation "com.cobblemon:neoforge:$rootProject.cobblemon_version"
    forgeRuntimeLibrary("thedarkcolour:kotlinforforge-neoforge:$rootProject.kotlin_for_forge_version") {
        exclude group: "net.neoforged.fancymodloader", module: "loader"
    }
    modImplementation "software.bernie.geckolib:geckolib-neoforge-$rootProject.geckolib_version"
    include(modApi "me.shedaniel.cloth:cloth-config-neoforge:$rootProject.cloth_config_version")

    modRuntimeOnly "maven.modrinth:jade:$rootProject.jade_version+neoforge"
    // modRuntimeOnly "lol.bai:badpackets:neo-$rootProject.badpackets_version"
    // modRuntimeOnly "mcp.mobius.waila:wthit:neo-$rootProject.wthit_version"
    modRuntimeOnly "io.wispforest:accessories-neoforge:1.1.0-beta.52+1.21.1"
    forgeRuntimeLibrary("blue.endless:jankson:1.2.2")
    forgeRuntimeLibrary("io.wispforest:endec:0.1.5.1")
    forgeRuntimeLibrary("io.wispforest.endec:gson:0.1.3.1")
    forgeRuntimeLibrary("io.wispforest.endec:jankson:0.1.3.1")
    forgeRuntimeLibrary("io.wispforest.endec:netty:0.1.2")
    modImplementation "curse.maven:cobblemon-mega-showdown-1189523:7523097"
    modCompileOnly "curse.maven:cobbledollars-859232:6604564"
    modCompileOnly "curse.maven:distant-horizons-508933:6791190"
    modImplementation "curse.maven:radical-cobblemon-trainers-api-1152792:7257753"
    modRuntimeOnly "dev.architectury:architectury-neoforge:13.0.8"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    var replaceProperties = [
        display_name: rootProject.display_name,
        description: rootProject.mod_description,
        archives_name: rootProject.archives_name,
        version: project.version,
        minecraft_version: rootProject.minecraft_version,
        cobblemon_version: rootProject.cobblemon_dependency,
        geckolib_version: rootProject.geckolib_dependency,
        neoforge_version: rootProject.neoforge_version
    ]
    inputs.properties replaceProperties

    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }

    filesMatching('pack.mcmeta') {
        expand  display_name: rootProject.display_name
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = "dev-shadow"
}

remapJar {
    atAccessWideners.add("cobblemonraiddens.accesswidener")

    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}