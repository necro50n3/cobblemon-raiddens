plugins {
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'org.jetbrains.kotlin.jvm' version '2.2.10'
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    idea {
        module {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    base {
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        maven { url = "https://maven.impactdev.net/repository/development/" }
        maven { url = "https://thedarkcolour.github.io/KotlinForForge/"}
        maven { url = "https://api.modrinth.com/maven" }
        maven { url = "https://jitpack.io" }
        maven {
            url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
            content {
                includeGroup("software.bernie.geckolib")
            }
        }
        maven { url 'https://maven.wispforest.io/releases' }
        maven { url 'https://maven.su5ed.dev/releases' }
        maven {
            url "https://maven2.bai.lol"
            content {
                includeGroup "lol.bai"
                includeGroup "mcp.mobius.waila"
            }
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }
}