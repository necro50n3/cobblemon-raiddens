plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

loom {
    enableTransitiveAccessWideners.set(true)
    accessWidenerPath = project(":common").loom.accessWidenerPath

    runs {
        datagen {
            inherit server
            "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":common").file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=cobblemonraiddens"
            runDir "build/datagen"
        }
    }
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
    mavenCentral()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation "net.fabricmc:fabric-language-kotlin:$rootProject.fabric_kotlin"
    modImplementation "com.cobblemon:fabric:$rootProject.cobblemon_version"
    modImplementation "software.bernie.geckolib:geckolib-fabric-$rootProject.geckolib_version"
    include(modApi "me.shedaniel.cloth:cloth-config-fabric:$rootProject.cloth_config_version")
    modRuntimeOnly "io.wispforest:accessories-fabric:1.1.0-beta.52+1.21.1"
    modImplementation "curse.maven:cobblemon-mega-showdown-1189523:7523096"
    modCompileOnly "curse.maven:cobbledollars-859232:6604561"
    modImplementation "maven.modrinth:supermartijn642s-config-lib:1.1.8-fabric-mc1.21"
    modImplementation "dev.architectury:architectury-fabric:13.0.8"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    var replaceProperties = [
        display_name: rootProject.display_name,
        description: rootProject.mod_description,
        archives_name: rootProject.archives_name,
        version: project.version,
        minecraft_version: rootProject.minecraft_version,
        cobblemon_version: rootProject.cobblemon_dependency
    ]
    inputs.properties replaceProperties

    filesMatching('fabric.mod.json') {
        expand  replaceProperties
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
}